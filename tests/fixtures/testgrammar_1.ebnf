"EBNF defined in itself." {
  syntax     = [ title ] "{" { rule } "}" [ comment ] .
  rule       = identifier ( "=" | ":" | ":==" ) expression ( "." | ";" ) .
  expression = term { "|" term } .
  term       = factor { factor } .
  factor     = identifier
             | literal
             | range
             | "[" expression "]"
             | "(" expression ")"
             | "{" expression "}" .
  identifier = character { character } .
  range      = character ".." character .
  title      = literal .
  comment    = literal .
  literal    = "'" character { character } "'"
             | '"' character { character } '"' .
  character  = "a" .. "z"
             | "A" .. "Z"
             | "0" .. "9" .
}